import org.cloudifysource.cosmo.cep.Message
import org.cloudifysource.cosmo.cep.AppInfo

declare Message
@role(event)
@timestamp(timestamp.getTime())
end

declare AppInfo
@role(event)
@timestamp(timestamp.getTime())
end

rule "Change message type after one minute"
    dialect "mvel"
when
    $appinfo:AppInfo()
    $message:Message( type == "before", $msgtext : msgtext, this after[ 1m ] $appinfo )
then
    System.out.println( $msgtext );
    $message.type = "after";
    channels["output"].send( $message );
end

rule "Create new message if not exits within 1 minute"
    dialect "mvel"
when
    $request: Message( type == "request")
    not Message( type == "response", this after[0, 1m ] $request )
then
    System.out.println( "Found no response.");
    $message = new Message()
    $message.type = "missing";
    $message.msgtext = "This is the message text";
    channels["output"].send( $message );
end